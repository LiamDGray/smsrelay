apply plugin: 'com.android.application'

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    useLibrary 'org.apache.http.legacy'
    defaultConfig {
        applicationId "com.advarisk.smsrelay"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 2
        versionName getVersionName()
    }
    signingConfigs {
        if (rootProject.file('signing.properties').exists()) {
            def props = new Properties()
            props.load(new FileInputStream(rootProject.file('signing.properties')))
            release {
                storeFile     rootProject.file(props.keyStore)
                storePassword props.keyStorePassword
                keyAlias      props.keyAlias
                keyPassword   props.keyPassword
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled   true
            zipAlignEnabled true
            if (rootProject.file('signing.properties').exists()) {
                signingConfig signingConfigs.release
            }
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
